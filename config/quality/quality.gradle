/**
 * Set up Findbugs and PMD to perform extensive code analysis.
 *
 * Gradle tasks added:
 * - findbugs
 * - pmd
 *
 * Both tasks above are added as dependencies of the check task so running check will
 * run all of them.*/

apply plugin: 'findbugs'
apply plugin: 'pmd'

def qualityConfigDir = "$project.rootDir/config/quality";
def reportsDir = "$project.buildDir/reports"

check.dependsOn 'findbugs', 'pmd'

task findbugs(type: FindBugs,
    group: 'Verification',
    description: 'Inspect java bytecode for bugs',
    dependsOn: ['compileDebugSources', 'compileReleaseSources']) {

  ignoreFailures = false
  effort = "max"
  reportLevel = "high"
  excludeFilter = new File("$qualityConfigDir/findbugs/android-exclude-filter.xml")
  classes = files("$project.rootDir/app/build/intermediates/classes")

  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  reports {
    xml.enabled = true
    html.enabled = false
    xml {
      destination "$reportsDir/findbugs/findbugs.xml"
    }
    html {
      destination "$reportsDir/findbugs/findbugs.html"
    }
  }

  classpath = files()
}


task pmd(type: Pmd, group: 'Verification', description: 'Inspect sourcecode for bugs') {
  ruleSetFiles = files("$qualityConfigDir/pmd/pmd-ruleset.xml")
  ignoreFailures = false
  ruleSets = []

  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  reports {
    xml.enabled = true
    html.enabled = true
    xml {
      destination "$reportsDir/pmd/pmd.xml"
    }
    html {
      destination "$reportsDir/pmd/pmd.html"
    }
  }
}